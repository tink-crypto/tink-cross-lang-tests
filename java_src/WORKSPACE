workspace(name = "testing_java")

load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

local_repository(
    name = "tink_java",
    path = "../../tink_java",
)

local_repository(
    name = "tink_java_gcpkms",
    path = "../../tink_java_gcpkms",
)

local_repository(
    name = "tink_java_awskms",
    path = "../../tink_java_awskms",
)

local_repository(
    name = "tink_java_hcvault",
    path = "../../tink_java_hcvault",
)

# To use `force_version`.
RULES_JVM_EXTERNAL_TAG = "6.1"

http_archive(
    name = "rules_jvm_external",
    sha256 = "08ea921df02ffe9924123b0686dc04fd0ff875710bfadb7ad42badb931b0fd50",
    strip_prefix = "rules_jvm_external-%s" % RULES_JVM_EXTERNAL_TAG,
    url = "https://github.com/bazelbuild/rules_jvm_external/releases/download/%s/rules_jvm_external-%s.tar.gz" % (RULES_JVM_EXTERNAL_TAG, RULES_JVM_EXTERNAL_TAG),
)

load("@tink_java//:tink_java_deps.bzl", "TINK_MAVEN_ARTIFACTS", "tink_java_deps")

tink_java_deps()

load("@tink_java//:tink_java_deps_init.bzl", "tink_java_deps_init")

tink_java_deps_init()

load("@tink_java_awskms//:tink_java_awskms_deps.bzl", "TINK_JAVA_AWSKMS_MAVEN_ARTIFACTS")
load("@tink_java_gcpkms//:tink_java_gcpkms_deps.bzl", "TINK_JAVA_GCPKMS_MAVEN_ARTIFACTS")
load("@tink_java_hcvault//:tink_java_hcvault_deps.bzl", "TINK_JAVA_HCVAULT_MAVEN_ARTIFACTS")

_GRPC_VERSION = "1.70.0"

http_archive(
    name = "io_grpc_grpc_java",
    sha256 = "39d2c8a9a21a15774247b0531267c98bc49ddc453a4bb2e391c2fa3ff1e15033",
    strip_prefix = "grpc-java-%s" % _GRPC_VERSION,
    url = "https://github.com/grpc/grpc-java/archive/v%s.zip" % _GRPC_VERSION,
)

load("@io_grpc_grpc_java//:repositories.bzl", "IO_GRPC_GRPC_JAVA_ARTIFACTS", "grpc_java_repositories")
load("@rules_jvm_external//:defs.bzl", "maven_install")
load("@rules_jvm_external//:specs.bzl", "maven")

grpc_java_repositories()

# gRPC direct deps used by this target.
_GRPC_DEPS = [
    "io.grpc:grpc-api:%s" % _GRPC_VERSION,
    "io.grpc:grpc-inprocess:%s" % _GRPC_VERSION,
    "io.grpc:grpc-protobuf:%s" % _GRPC_VERSION,
    "io.grpc:grpc-stub:%s" % _GRPC_VERSION,
    "io.grpc:grpc-netty-shaded:%s" % _GRPC_VERSION,
    "io.grpc:grpc-testing:%s" % _GRPC_VERSION,
    # This is needed to add grpc-core while removing a circular dependency
    # between grpc-core and grpc-util.
    #
    # See https://github.com/grpc/grpc-java/issues/10576.
    maven.artifact(
        artifact = "grpc-core",
        exclusions = [
            "io.grpc:grpc-util",
        ],
        group = "io.grpc",
        version = _GRPC_VERSION,
    ),
]

maven_install(
    artifacts = TINK_MAVEN_ARTIFACTS +
                TINK_JAVA_GCPKMS_MAVEN_ARTIFACTS +
                TINK_JAVA_AWSKMS_MAVEN_ARTIFACTS +
                TINK_JAVA_HCVAULT_MAVEN_ARTIFACTS +
                IO_GRPC_GRPC_JAVA_ARTIFACTS +
                _GRPC_DEPS + [
        # This is needed because `com.google.cloud.kms.v1.EncryptRequest` used in tink-java-gcpkms
        # depends on `GeneratedMessageV3` [1] which was renamed in 26.0 [2].
        #
        # [1] https://cloud.google.com/java/docs/reference/google-cloud-kms/2.46.0/com.google.cloud.kms.v1.EncryptRequest
        # [2] https://github.com/protocolbuffers/protobuf/releases/tag/v26.0
        maven.artifact(
            artifact = "protobuf-java",
            force_version = True,
            group = "com.google.protobuf",
            version = "3.25.5",
        ),
        "args4j:args4j:2.33",
    ],
    generate_compat_repositories = True,
    repositories = [
        "https://maven.google.com",
        "https://repo1.maven.org/maven2",
    ],
)

load("@maven//:compat.bzl", "compat_repositories")

compat_repositories()
