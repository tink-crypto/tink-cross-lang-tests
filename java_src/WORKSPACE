workspace(name = "testing_java")

load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "tink_java",
    urls = ["https://github.com/tink-crypto/tink-java/archive/main.zip"],
    strip_prefix = "tink-java-main",
)

http_archive(
    name = "tink_java_gcpkms",
    urls = ["https://github.com/tink-crypto/tink-java-gcpkms/archive/main.zip"],
    strip_prefix = "tink-java-gcpkms-main",
)

http_archive(
    name = "tink_java_awskms",
    urls = ["https://github.com/tink-crypto/tink-java-awskms/archive/main.zip"],
    strip_prefix = "tink-java-awskms-main",
)

load("@tink_java//:tink_java_deps.bzl", "TINK_MAVEN_ARTIFACTS", "tink_java_deps")

tink_java_deps()

load("@tink_java//:tink_java_deps_init.bzl", "tink_java_deps_init")

tink_java_deps_init()

load("@tink_java_gcpkms//:tink_java_gcpkms_deps.bzl", "TINK_JAVA_GCPKMS_MAVEN_ARTIFACTS")

load("@tink_java_awskms//:tink_java_awskms_deps.bzl", "TINK_JAVA_AWSKMS_MAVEN_ARTIFACTS")

# Required by gRPC, which would fetch v3.21.7 otherwise.
http_archive(
    name = "com_google_protobuf_javalite",
    sha256 = "6b6bf5cd8d0cca442745c4c3c9f527c83ad6ef35a405f64db5215889ac779b42",
    strip_prefix = "protobuf-3.19.3",
    urls = ["https://github.com/protocolbuffers/protobuf/archive/v3.19.3.zip"],
)

# Release from 2022-04-28.
http_archive(
    name = "io_grpc_grpc_java",
    sha256 = "c1b80883511ceb1e433fb2d4b2f6d85dca0c62a265a6a3e6695144610d6f65b8",
    strip_prefix = "grpc-java-1.46.0",
    url = "https://github.com/grpc/grpc-java/archive/v1.46.0.tar.gz",
    # Replaces `protobuf_java_lite` with `protobuf_javalite`. Protobuf v3.19.3
    # doesn't expose a `protobuf_java_lite` target.
    patches = ["@testing_java//third_party:io_grpc_grpc_java_1_46_0.diff"],
    patch_args = ["-p1"],
)

load("@rules_jvm_external//:defs.bzl", "maven_install")
load("@io_grpc_grpc_java//:repositories.bzl", "IO_GRPC_GRPC_JAVA_ARTIFACTS", "IO_GRPC_GRPC_JAVA_OVERRIDE_TARGETS", "grpc_java_repositories")

grpc_java_repositories()

maven_install(
    artifacts = TINK_MAVEN_ARTIFACTS +
      TINK_JAVA_GCPKMS_MAVEN_ARTIFACTS +
      TINK_JAVA_AWSKMS_MAVEN_ARTIFACTS +
      IO_GRPC_GRPC_JAVA_ARTIFACTS + [
        "args4j:args4j:2.33",
      ],
    generate_compat_repositories = True,
    override_targets = IO_GRPC_GRPC_JAVA_OVERRIDE_TARGETS,
    repositories = [
        "https://maven.google.com",
        "https://repo1.maven.org/maven2",
    ],
)

load("@maven//:compat.bzl", "compat_repositories")

compat_repositories()
